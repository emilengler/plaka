NL = _{ "\n" }
// TODO: Consider using EOI
Document      =  { (Item | NL)+ }
Item          =  { KeywordLine ~ Object? }
KeywordLine   =  { Keyword ~ (WS ~ Argument)* ~ NL }
Keyword       =  { KeywordStart ~ KeywordChar* }
KeywordObject =  { KeywordStart+ }
KeywordStart  = _{ ASCII_ALPHANUMERIC }
KeywordChar   = _{ KeywordStart | "-" }
Argument      =  { ArgumentChar+ }
ArgumentChar  = _{ '\x21'..'\x7E' }
WS            = _{ (" " | "\t")+ }
Object        =  { BeginLine ~ Base64 ~ EndLine }
BeginLine     =  { "-----BEGIN " ~ KeywordObject ~ (" " ~ KeywordObject)* ~ "-----" ~ NL }
EndLine       =  { "-----END " ~ KeywordObject ~ (" " ~ KeywordObject)* ~ "-----" ~ NL }
Base64        =  { (ASCII_ALPHANUMERIC | "+" | "/" | "=" | NL)+ }
